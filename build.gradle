plugins {
    id "com.energizedwork.webdriver-binaries" version "1.4"
}

group 'com.learnqa'
version '1.0-SNAPSHOT'

apply plugin: 'java'

sourceCompatibility = 1.8

dependencies {
    compile 'org.seleniumhq.selenium:selenium-java:3.12.0'
    compile 'org.seleniumhq.selenium:selenium-api:3.12.0'
    compile group: 'org.testng', name: 'testng', version: '6.14.3'
    compile group: 'commons-io', name: 'commons-io', version: '2.6'
    compile group: 'net.lightbody.bmp', name: 'browsermob-core', version: '2.1.5'
}

repositories {
    mavenLocal()
    mavenCentral()
}

import org.apache.tools.ant.taskdefs.condition.Os

webdriverBinaries {
    chromedriver '2.42'
    geckodriver '0.23.0'
    iedriverserver '3.8.0'
}

test {
    testLogging {
        events "passed", "skipped", "failed"
        exceptionFormat "full"
    }
    useTestNG(){
        suites "TestNG.xml"
        options {
            parallel = 'classes'
            threadCount = 2
        }
        //set TestNG output dir
        outputDirectory = file("$project.buildDir//testngOutput")
        //required to turn on TestNG reports
        useDefaultListeners = true
    }
    systemProperties = [
        REMOTE_DRIVER: System.getProperty('REMOTE_DRIVER', 'false')
    ]
}

task setupZalenium(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'scripts/zalenium-start.bat'
    } else {
        commandLine './scripts/zalenium-start.sh'
    }
}

task setupSelenoid(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'scripts/selenoid-start.bat'
    } else {
        commandLine './scripts/selenoid-start.sh'
    }
}

task tearDownZalenium(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'scripts/zalenium-stop.bat'
    } else {
        commandLine './scripts/zalenium-stop.sh'
    }
}

task tearDownSelenoid(type: Exec) {
    if (Os.isFamily(Os.FAMILY_WINDOWS)) {
        commandLine 'cmd', '/c', 'scripts/selenoid-stop.bat'
    } else {
        commandLine './scripts/selenoid-stop.sh'
    }
}

if (project.hasProperty("dockerSetup")){
    if(project.property("dockerSetup") == "zalenium") {
        test.dependsOn setupZalenium
        test.finalizedBy tearDownZalenium
    } else if(project.property("dockerSetup") == "selenoid") {
        test.dependsOn setupSelenoid
        test.finalizedBy tearDownSelenoid
    }
}
